#!/usr/bin/env perl

use strict;
use lib $ENV{'QCHASM'};
use lib "$ENV{QCHASM}/AaronTools/bin";

use Getopt::Long;
use Pod::Usage;
use AaronTools::Geometry;
require _utils;

sub main {
    my %opt  = %{ shift(@_) };
    my @files = @_;
    my $overwrite = 1;

    $opt{output} = _utils::handle_overwrite( $opt{output} ) unless $opt{force};
    _utils::mkdirsfor( $opt{output} ) if $opt{output};
    for my $file (@files) {
        my $geo = new AaronTools::Geometry();
    
        $geo->{name} = $file;
    
        $geo->read_geometry($file)
          or die("File may be damaged, cannot read geometry\n");
    
        if($opt{comment}) {
            $geo->printXYZ( $opt{output}, $opt{comment}, $overwrite );
        } else {
            $geo->printXYZ( $opt{output}, "", $overwrite );
        }
        $overwrite = 0;
    }

    return 0;
}

my %opt;
GetOptions( 'help|h'     => \$opt{help},
            'force|f'    => \$opt{force},
            'comment|c=s'=> \$opt{comment},
            'output|o=s' => \$opt{output} )
  or pod2usage(
    { -exitval => 1,
      -verbose => 1
    } );
pod2usage(0) if $opt{help};
pod2usage(
    { -message => "Please specify a filename\n",
      -exitval => 1,
      -verbose => 1
    } ) if @ARGV < 1;

&main( \%opt, @ARGV );

=pod

=head1 SYNOPSIS

grab_coords [options] file1 [file2 file3]

=head2 SYNOPSIS

Retrieves final set of coordinates from XYZ, Gaussian output files (.log), Gaussian input files (.com), or PDB files.

=head1 OPTIONS

=over

=item B<-h>, B<--help>

Print this help message and exit

=item B<-o FILENAME [f]>, B<--output FILENAME [--force]>

Write output to FILENAME (in xyz format). Optionally, force overwrite if FILENAME exists.

=item B<-c COMMENT>, B<--comment COMMENT>

Print COMMENT in comment line

=back

=cut

